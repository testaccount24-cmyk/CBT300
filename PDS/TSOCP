         TITLE '   T S O C P  '
***********************************************************************
*                                                                     *
*        'TSOCP'                                                      *
*                                                                     *
***********************************************************************
         SPACE
* THIS TSO COMMAND RUNS A SPECIFIED MEMBER OF THE PDS ALLOCATED
* TO DDNAME 'TASKLIB' AS A TSO COMMAND.
* ------------------------------------------------------------------- *
* THE IDEA HERE IS TO BE ABLE TO RUN A TSO COMMAND FROM AN ARBITRARY
* LIBRARY, IN TSO READY MODE, WITHOUT THE LOGON PROC HAVING
* PRE-ALLOCATED THE LIBRARY.  HOWEVER, THE TSOCP PROGRAM ITSELF,
* MUST BE ACCESSIBLE TO THE TSO SESSION ALREADY.
*
* EXECUTION:
*
* ALLOC F(TASKLIB) DA(YOUR.TSO.COMMAND.LIBRARY) SH REUSE
* TSOCP MEMNAME
* ------------------------------------------------------------------- *
* TO MAKE THIS COMMAND EASIER TO USE, ONE MAY WRITE A CLIST AS FOLLOWS
*
* PROC 1 CMD
* ALLOC F(TASKLIB) DA('YOUR.LOAD.LIBRARY') SH REUSE
* TSOCP &CMD
*
* (BETTER NOT TO FREE DD TASKLIB AFTER JUST ONE USE--YOUR CHOICE.)
* ------------------------------------------------------------------- *
* SYNTAX: TSOCP MEMBER (OPERANDS IF ANY)
* MODIFIED SOURCE CODE FROM CBTTAPE FILE 300 MEMBERS 'CMD' AND 'AUPGM1'
* UPDATED DECEMBER 4 2022 TO ADD TASKLIB.
*
         SPACE
         MACRO
&NAME    MOVE  &TO,&TOL,&FROM,&FROML
         LCLB  &B(8)               0=FALSE 1=TRUE
&B(1)    SETB  (T'&TO EQ 'O')
&B(2)    SETB  (T'&TOL EQ 'O')
&B(3)    SETB  (T'&FROM EQ 'O')
&B(4)    SETB  (T'&FROML EQ 'O')
         AIF   (&B(1) OR &B(2) OR &B(3)).REQOM
&B(5)    SETB  ('&TO'(1,1) EQ '(')
&B(6)    SETB  ('&TOL'(1,1) EQ '(')
&B(7)    SETB  ('&FROM'(1,1) EQ '(')
         AIF   (&B(4)).NOB8
&B(8)    SETB  ('&FROML'(1,1) EQ '(')
.NOB8   ANOP
         AIF   (NOT(&B(5) AND &B(6) AND &B(7))).NALLR
         AIF   (NOT &B(4) AND NOT &B(8)).NALLR
         LR    R0,&TO(1)           RECEIVING FIELD
         LR    R1,&TOL(1)          RECEIVING LENGTH
         LR    R14,&FROM(1)        SENDING FIELD
         AIF   (&B(4)).NOFROML
         LR    R15,&FROML(1)       SENDING LENGTH
         ICM   R15,B'1000',=X'40'  PAD IF SENDING IS SHORTER
         AGO   .MVCL
.NOFROML LR    R15,&TOL(1)         SENDING LENGTH
.MVCL    MVCL  R0,R14
         MEXIT
.REQOM   MNOTE 4,'REQUIRED OPERAND(S) OMITTED'
         MEXIT
.NALLR   MNOTE 4,'ALL OPERANDS MUST BE IN REGISTER FORMAT (PARENS)'
         MEND
         EJECT
TSOCP    START
         USING *,R12
         B     @PROLOG-*(,15)
         DC    AL1(11),CL11'TSOCP'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(0),AL3(@DATAL) SUBPOOL AND LENGTH
@PROLOG  STM   14,12,12(R13)
         LR    R12,R15
         LR    R2,R1
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         LR    R9,R1
         USING @DATA,R9
         SPACE 1
         LR    R0,R9               AREA TO BE CLEARED
         L     R1,@SIZE            LENGTH TO BE CLEARED
         SR    R14,R14             CLEAR R14 AND R15
         SR    R15,R15             CLEAR R14 AND R15
         MVCL  R0,R14              ZERO IT ALL
         SPACE 1
         LR    R1,R9               POINT TO NEW SAVEAREA
         ST    R13,4(,R1)          PUT OLD ADDRESS IN NEW SAVE AREA
         ST    R1,8(,R13)          PUT NEW ADDRESS IN OLD SAVE AREA
         LR    R13,R1              SWITCH R13 TO NEW SAVE AREA
         SR    R10,R10             TASKLIB DD
         SPACE
************************************************************
*                                                          *
*         GETMAIN A NEW COMMAND BUFFER                     *
*                                                          *
************************************************************
         SPACE
         USING CPPL,R2
         L     R3,CPPLECT
         USING ECT,R3
         TM    ECTSWS,ECTNOPD      ANY OPERANDS?
         BO    NOPERAND            NO - BRANCH
         DROP  R3
         SPACE
         L     R3,CPPLCBUF
         CLC   2(2,R3),=H'0'       IS THERE AN OFFSET?
         BE    NOPERAND            NO - BRANCH
         LH    R4,0(,R3)           GET CBUF LENGTH
         SH    R4,2(,R3)           SUBTRACT OFFSET
         CH    R4,=H'4'            DOES OFFSET = LENGTH?
         BNH   NOPERAND            YES - NO OPERANDS
         LR    R0,R4
         CH    R0,=H'256'          WILL 256 BYTES BE ADDRESSABLE
         BNL   *+8                 YES, BRANCH
         LH    R0,=H'256'          NO, MAKE AT LEAST 256 BYTES
         O     R0,SUBPOOL
         ST    R0,FREECBUF
         GETMAIN R,LV=(0)
         ST    R1,FREECBUF+4
         LR    R8,R1
         XC    0(4,R8),0(R8)
         STH   R4,0(,R8)
         SH    R4,=H'4'            SUBTRACT PREFIX LENGTH
         AH    R3,2(,R3)           POINT TO OPERAND - 4
         AH    R3,=H'4'            POINT TO OPERAND
         SPACE
         LA    R7,4(,R8)           POINT TO RECEIVING FIELD
         SPACE
*        LR    R1,R4
*        BCTR  R1,0
*        B     *+10
*        MVC   0(0,R7),0(R3)
*        EX    R1,*-6
         SPACE
         MOVE  (R7),(R4),(R3)
         SPACE
         MVC   NEWCPPL(16),0(R2)   COPY CPPL FOR NEW CBUF
         LA    R2,NEWCPPL
         ST    R8,CPPLCBUF
         SPACE
************************************************************
*                                                          *
*         CALL THE SCAN SERVICE ROUTINE                    *
*                                                          *
************************************************************
         SPACE
         LA    R1,MYCSPL
         USING CSPL,R1
         MVC   CSPLUPT(4),CPPLUPT
         MVC   CSPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,CSPLECB
         XC    MYECB,MYECB
         LA    R0,MYCSFLAG
         ST    R0,CSPLFLG
         LA    R0,MYCSOA
         ST    R0,CSPLOA
         XC    MYCSFLAG(4),MYCSFLAG
         ST    R8,CSPLCBUF
         XC    LINKAREA(8),LINKAREA
         SPACE
         L     R15,16              CVTPTR
         TM    X'1E0'(R15),X'80'   IF HI ORDER BIT ON
         BNO   SCANLINK            THEN DO LINK
         L     R15,X'1E0'(,R15)    GET ADDR OF IKJSCAN
         BALR  R14,R15
         B     SCANEXIT
SCANLINK EQU   *
         LINK  EP=IKJSCAN,SF=(E,LINKAREA)
SCANEXIT EQU   *
         SPACE
         LTR   R15,R15
         BNZ   SCANERR
         LA    R1,MYCSPL
         L     R15,CSPLOA
         DROP  R1                  CSPL
         USING CSOA,R15
         TM    CSOAFLG,CSOAVWP+CSOAVNP
         BZ    SCANSYNT
         SPACE
         L     R3,CPPLECT
         USING ECT,R3
         TM    CSOAFLG,CSOAVWP
         BO    VWP
         OI    ECTSWS,ECTNOPD
         B     SWSET
VWP      NI    ECTSWS,255-ECTNOPD
         DROP  R3                  ECT
SWSET    EQU   *
         SPACE
         TM    CSOAFLG,CSOAEXEC    IMPLICIT (PERCENT SIGN)
         BO    IMPLEXEC            YES - BRANCH
         SPACE
************************************************************
*                                                          *
*         OPEN DCB FOR DDNAME TASKLIB                      *
*                                                          *
************************************************************
         SPACE
         ST    R15,SAVECSOA
         SR    R10,R10
         LA    R1,TASKDCB
         MVC   DEVDDNAM,DCBDDNAM-IHADCB(R1) 'TASKLIB'
         DEVTYPE DEVDDNAM,DEVDATA
         LTR   R15,R15             IF NO TASKLIB DD
         BNZ   NOTASK                LEAVE R10 AS ZERO
         MVC   TASKDCW(TASKLEN),TASKDCB
         LA    R10,TASKDCW
         MVI   OPENL,X'80'
         LA    R1,OPENL
         OPEN  ((R10),(INPUT)),MF=(E,(1))
         TM    DCBOFLGS-IHADCB(R10),DCBOFOPN
         BNO   OPENERR
NOTASK   L     R15,SAVECSOA
         SPACE
************************************************************
*                                                          *
*         ISSUE BLDL FOR COMMAND NAME FOUND BY SCAN        *
*                                                          *
************************************************************
         SPACE
         L     R1,CSOACNM
         LH    R14,CSOALNM
         DROP  R15                 CSOA
         MVC   BLDL(64),BLDLMODL
         MVC   BLDLNAME(8),=CL8' '
         BCTR  R14,0
         B     *+10
         MVC   BLDLNAME(0),0(R1)
         EX    R14,*-6
         SPACE
         BLDL  (R10),BLDL
         SPACE
         LTR   R15,R15
         BNZ   BLDLERR
         SPACE
************************************************************
*                                                          *
*         CALL THE COMMAND                                 *
*                                                          *
************************************************************
         SPACE
         LR    R1,R2
*-       XC    LINKAREA(8),LINKAREA
*-       LINK  EPLOC=BLDLNAME,DCB=(R10),SF=(E,LINKAREA)
         MVC   ATCHW(ATCHLEN),ATCHM
         XC    ATCHECB,ATCHECB
         ATTACH EPLOC=BLDLNAME,SF=(E,ATCHW),MF=(E,(1)),                +
               TASKLIB=(R10),ECB=ATCHECB
         ST    R1,ATCHTCBP        SAVE TCB POINTER
         WAIT  ECB=ATCHECB
         LA    R1,ATCHTCBP
         DETACH (1)
         L     R15,ATCHECB         RETURN CODE
NOATT    EQU   *
         B     EXIT
         SPACE
************************************************************
*                                                          *
*         ISSUE ERROR MESSAGES                             *
*                                                          *
************************************************************
         SPACE
NOPERAND EQU   *
         LA    R1,MSGNOPER
         LA    R0,L'MSGNOPER
         BAL   R14,PUTLINE
         B     EXIT12
SCANERR  EQU   *
         LA    R1,MSGSCANE
         LA    R0,L'MSGSCANE
         BAL   R14,PUTLINE
         B     EXIT12
SCANSYNT EQU   *
         LA    R1,MSGSCANS
         LA    R0,L'MSGSCANS
         BAL   R14,PUTLINE
         B     EXIT12
OPENERR  SR    R10,R10             DON'T CLOSE
         LA    R1,MSGOPEN
         LA    R0,L'MSGOPEN
         BAL   R14,PUTLINE
         B     EXIT12
         SPACE
IMPLOPND LR    R4,R2               NEW CPPL POINTER
         L     R6,4(,R13)          ORIGINAL SAVEAREA
         L     R6,24(,R6)          RESTORE ORIGINAL CPPL POINTER
         L     R2,0(R6)            POINT TO ORIGINAL CBUF
         L     R4,0(R4)            POINT TO NEW CBUF
         LH    R3,0(,R2)           ORIGINAL LENGTH
         LH    R5,0(,R4)           NEW LENGTH
         MOVE  (R2),(R3),(R4),(R5) MOVE NEW TO OLD AND PAD
*                                  NOTE REGS NOT ALTERED BY MOVE
*                                  BECAUSE WORKREGS USED IN MVCL
         STH   R3,0(,R2)           RESTORE ORIGINAL LENGTH
IMPLEXEC LR    R1,R2               NEW CPPL
         L     R15,0(,R1)          CPPLCBUF
         XC    2(2,R15),2(R15)     SET CBUF OFFSET TO IMPLICIT EXEC
         L     R15,12(,R1)         CPPLECT
         CLI   20(R15),X'40'       ECTSCMD BLANK?
         BNE   *+10                YES - SUBCOMMAND NOT FOUND
         MVC   12(8,R15),=CL8'EXEC' NO - COMMAND NOT FOUND
         L     R0,@SIZE
         LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN R,A=(1),LV=(0)
         L     R1,24(,R13)         RESTORE CPPL POINTER
         LR    R1,R2               USE NEW CPPL
         LA    R15,20(,R13)        POINT TO 8 BYTE WORKAREA
         XC    0(8,R15),0(R15)     ZERO WORKAREA
         XCTL  (2,12),EP=EXEC,SF=(E,(15))
         SPACE
BLDLERR  MVC   MSGBLDLW(L'MSGBLDLE),MSGBLDLE
         MVC   MSGBLDLW+8(8),BLDLNAME
         LA    R1,MSGBLDLW
         LA    R0,L'MSGBLDLE
         BAL   R14,PUTLINE
         B     EXIT12
         SPACE
PUTLINE  ST    R14,PUTLINS
         SVC   93
         L     R14,PUTLINS
         BR    R14
         SPACE
EXIT12   LA    R15,12
         SPACE 1
EXIT     LTR   R10,R10
         BZ    EXITNOC
         MVI   CLOSEL,X'80'
         LA    R1,CLOSEL
         CLOSE ((R10)),MF=(E,(1))
         SR    R10,R10
EXITNOC  LM    R0,R1,FREECBUF
         LTR   R0,R0
         BZ    EXITX
         LR    R2,R15
         FREEMAIN R,A=(1),LV=(0)
         LR    R15,R2
EXITX    LR    R1,R13
         L     R0,@SIZE
         L     R13,4(,R13)
         LR    R2,R15
         FREEMAIN R,A=(1),LV=(0)
         LR    R15,R2
         LM    0,12,20(13)
         L     R14,12(,R13)
         BR    R14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
MSGNOPER DC    C'REQUIRED OPERANDS MISSING'
MSGSCANE DC    C'SCAN ERROR'
MSGSCANS DC    C'INVALID COMMAND SYNTAX'
MSGOPEN  DC    C'OPEN FAILED FOR DDNAME TASKLIB'
MSGBLDLE DC    C'COMMAND ........ NOT FOUND'
         SPACE
BLDLMODL DC    H'1,74',CL8' ',XL4'00',CL62' '
         SPACE
SUBPOOL  DC    A(X'01000000')
         PRINT NOGEN
TASKDCB  DCB   DDNAME=TASKLIB,DSORG=PO,MACRF=R
TASKLEN  EQU   *-TASKDCB
         PRINT GEN
ATCHM    ATTACH SF=L
ATCHLEN  EQU   *-ATCHM
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
MYCSPL   DS    6F
MYCSFLAG DS    F
MYECB    DS    F
MYCSOA   DS    D
NEWCPPL  DS    4F
FREECBUF DS    2F
LINKAREA DS    2F
PUTLINS  DS    F
DEVDDNAM DS    D
DEVDATA  DS    2F
OPENL    DS    F
CLOSEL   DS    F
SAVECSOA DS    F
         DS    0D
BLDL     DS    F
BLDLNAME DS    CL8
         DS    CL62
TASKDCW  DS    0F,(TASKLEN)X
ATCHW    DS    0F,(ATCHLEN)X
ATCHECB  DS    F
ATCHTCBP DS    F
MSGBLDLW DS    CL64
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
         IKJCPPL
         SPACE
         IKJECT
         SPACE
         IKJCSPL
         SPACE
         IKJCSOA
         SPACE
         PRINT NOGEN
         DCBD  DSORG=PO
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
