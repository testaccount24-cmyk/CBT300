         TITLE 'MVS TSO "DAJOB" COMMAND TO DISPLAY ACTIVE JOBS'
*---------------------------------------------------------------------*
*
*        TSO "DAJOB" COMMAND TO LIST THE JOBNAMES OF ALL CURRENTLY
*        EXECUTING BATCH JOBS.
*
*        FOUND THIS LITTLE JEWEL AND TIDY'ED THE CODE UP. THIS CODE
*        WAS DONE IN 1978 AND STILL RUNS TODAY AS IS (MVS/XA 2.2.0).
*
*        JIM MARSHALL
*
*        OUTPUT:
*                                                S T E P  S T E P
*             JOBNAME  ASID  STEPNAME  PROCSTEP  TCB SEC  SRB SEC
*             -------  ----  --------  --------  -------  -------
*             JOB1       05  STEP001   GO            0.1      0.1
*             JOB9    S  06  STEPA     STEP1       123.5      2.5
*             JOB6       13  PLI       LKED          1.3     10.2
*             JOB4       23  GO        STEP10       12.8      0.5
*
*
*      NOTE:  THIS CODE WAS WRITTEN TO HANDLE 64 ASIDS AND I BUMPED
*             IT UP TO 72 TO HANDLE THE TSOIDS. NEEDS TO HAVE A MAJOR
*             CODE MODIFICATION TO MAKE IT WORK BEYOND. WILL DO IT
*             LATER.
*
*
* CHNGE LOG: 17MAY91 - ADDED TSO USERIDS TO THIS DISPLAY WITH PROCS
*                      NAMES TO SEE WHAT LOGON PROC USERS USED   JDM1
*            24MAY91 - PGM BUILT BUFFER WITH ALL RESULTS ONLY BIG
*                      ENOUGH FOR 72 LINES!!!  0C4 TIME          JDM2
*            28DEC12 - CSCB IS NOW ABOVE THE LINE - AMODE IS 31
*                      BLANK OUT HEX ZEROES IN REPORT
*                      CONVERT TPUT TO APUT (PUTLINE OUTPUT)     SBG1
*
*---------------------------------------------------------------------*
* ------------------------------------------------------------------- *
         MACRO
&LABEL   APUT  &MSG,                    @MESSAGE TEXT                  &
               &LEN,                    @LENGTH OF MESSAGE             &
               &RSA=,                   OPTIONAL 2 WORD SAVE AREA      &
               &EPA=                    EPUTL EPA
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.*  --> E@PUT
.*
.*  THIS MACRO SETS UP A PARAMETERS AND INVOKES THE EPUTL ROUTINE.
.*
.*
.*  ARGUMENTS: -----------------------------------------------------
.*
.*      &MSG : ADDRESS OF MESSAGE TEXT OR A LITERAL
.*
.*      &LEN : LENGTH OF MESSAGE TEXT (DEFAULT IS L'&MSG)
.*
.*      &RSA :(OPTIONAL) 1 WORD SAVE AREA TO PRESERVE R14 FOR TPUT
.*            COMPATIBILITY.
.*
.*      &EPA :(OPTIONAL) EPA FOR EPUTL ROUTINE
.*
.*
.*   EXAMPLES: -----------------------------------------------------
.*
.*      1) APUT  'LITERAL'
.*
.*      2) APUT  (R3),80
.*
.*      3) APUT  LABEL
.*
.*      4) APUT  LABEL,40
.*
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.*
.*  LOCAL SET SYMBOLS
.*
         LCLC  &SMA                     MESSAGE ADDRESS (REG)
         LCLC  &SML                     MESSAGE LENGTH (REG)
         LCLC  &SEP                     EPA (REGISTER)
.*
         LCLC  &L1,&L2,&L3              LOCAL LABELS
.*
&L1      SETC  'EP'.'&SYSNDX'.'L1'      LOCAL LABELS
&L2      SETC  'EP'.'&SYSNDX'.'L2'      LOCAL LABELS
&L3      SETC  'EP'.'&SYSNDX'.'L3'      LOCAL LABELS
.*
.*
.*  MESSAGE ADDRESS
.*
         AIF   (T'&LABEL EQ 'O').A0500                      *HMD 11/86*
.*                                                          *HMD 11/86*
&LABEL   DS    0H                                           *HMD 11/86*
.*                                                          *HMD 11/86*
.A0500   ANOP                                               *HMD 11/86*
         AIF   ('&MSG' NE '').A2000
         MNOTE 8,'''&MSG='' MISSING / REQUIRED'
         MEXIT
.*
.A2000   ANOP
         AIF   ('&MSG'(1,1) NE '(').A2400
&SMA     SETC  '&MSG'(2,K'&MSG-2)
         LR    R1,&SMA                  @MESSAGE TEXT
         AGO   .A2600
.*
.A2400   ANOP
         AIF   ('&MSG'(1,1) NE '''').A2500
         B     &L3                      BRANCH AROUND MESSAGE
&L2      DC    C&MSG
&L3      DS    0H
         LA    R1,&L2                   @MESSAGE
         AGO   .A2600
.*
.A2500   ANOP
         LA    R1,&MSG                  @MESSAGE TEXT
.A2600   ANOP
.*
.*  MESSAGE LENGTH
.*
         AIF   ('&LEN' NE '').A3000
         AIF   ('&MSG'(1,1) NE '''').A2800
         LA    R0,L'&L2                 MESSAGE LENGTH
         AGO   .A3600
.*
.A2800   ANOP
         AIF   ('&MSG'(1,1) EQ '(').A2900
         LA    R0,L'&MSG                MESSAGE LENGTH
         AGO   .A3600
.*
.A2900   ANOP
         MNOTE 8,'''LEN'' MISSING / REQUIRED'
         MEXIT
.*
.A3000   ANOP
         AIF   ('&LEN'(1,1) NE '(').A3400
&SML     SETC  '&LEN'(2,K'&LEN-2)
         LR    R0,&SML                  MESSAGE LENGTH
         AGO   .A3600
.*
.A3400   ANOP
         LA    R0,&LEN                  MESSAGE LENGTH
.A3600   ANOP
.*
.*  RSA
.*
         AIF   ('&RSA' EQ '').A4900
         ST    R14,&RSA                 SAVE REGS 14
.A4900   ANOP
.*
.*  ENTRY POINT
.*
         AIF   ('&EPA' NE '').A5000
         L     R15,=V(EPUTL)            EPA
         AGO   .A5600
.*
.A5000   AIF   ('&EPA'(1,1) NE '(').A5400
&SEP     SETC  '&EPA'(2,K'&EPA-2)
         LR    R15,&SEP                 EPA
         AGO   .A5600
.*
.A5400   ANOP
         L     R15,&EPA                 EPA
.*
.A5600   ANOP
.*
.*  CALL LINKAGE
.*
.ACALL   BALR  R14,R15                  CALL PUTLINE ROUTINE
.*
         AIF   ('&RSA' EQ '').A6900
         L     R14,&RSA                 RESTORE R14
.A6900   ANOP
.*
.AMEND   MEND
* ------------------------------------------------------------------- *
         SPACE
R0       EQU   0                       MVS
R1       EQU   1                       MVS
R2       EQU   2                       ADDR TO CVT
R3       EQU   3                       ADDR TO ASVT
R4       EQU   4                       DSECT TO OUTPUT LINE      JDM2
R5       EQU   5                       MVS
R6       EQU   6                       ADDR TO BASEA
R7       EQU   7                       ADDR TO CSCB
R8       EQU   8                       ADDR TO ASCB
R9       EQU   9                       WORK
R10      EQU   10                      WORK
R11      EQU   11                      WORK
R12      EQU   12                      BASE REG
R13      EQU   13                      SAVE AREA
R14      EQU   14                      MVS
R15      EQU   15                      MVS
         SPACE
*MODULE ATTRIBUTES: REENTRANT, REUSABLE
         SPACE
DAJOB    CSECT
DAJOB    RMODE 24
DAJOB    AMODE 31
*---------------------------------------------------------------------*
*        ESTABLISH LINKAGE CONVENTIONS AND SAVEAREA
*---------------------------------------------------------------------*
         PRINT NOGEN
         SAVE  (14,12),,DAJOB_&SYSDATE
         LR    R12,R15
         LR    R11,R15                 SAVE IT FOR DEBUGGING
         USING DAJOB,R12               ADDRESSABILITY
         LR    R4,R13
         GETMAIN R,LV=AREASIZE         OBTAIN WORK AREA
         LR    R13,R1                  SAVE AREA POINTER
         USING SAVEAREA,R13            ADDRESSABILITY
         ST    R4,4(R13)               STORE SAVEAREA ADDRESS
         SPACE
         MVI   BLANKS-1,X'40'                                    SBG1
         MVC   BLANKS,BLANKS-1                                   SBG1
         MVI   ZEROES-1,X'00'                                    SBG1
         MVC   ZEROES,ZEROES-1                                   SBG1
*---------------------------------------------------------------------*
*        LOCATE NECESSARY CONTROL BLOCKS
*---------------------------------------------------------------------*
         SPACE
         L     R2,CVTPTR               CVT ADDRESS
         USING CVT,R2                  ADDRESSABILITY
         L     R3,CVTASVT              ASVT POINTER
         USING ASVT,R3                 ADDRESSABILITY
         SR    R9,R9                   CLEAR REG 9
         LA    R4,ITEM                 OUTPUT AREA               JDM2
         USING ITEM,R4                 ADDRESSABILITY
         LA    10,ASVTENTY             ADDRESS OF FIRST ENTRY
         SPACE 1                                                 JDM2
*----------------------------------------------------------------JDM2-*
*        PUT OUT THE TITLES                                      JDM2
*----------------------------------------------------------------JDM2-*
         SPACE 1                                                 JDM2
         LA    R1,TITLE                POINT TO TITLE            JDM1
         LA    R0,L'TITLE              GET ITS LENGTH            JDM1
         APUT  (R1),(R0),R             WRITE HEADER LINE         JDM1
         LA    R1,TITLE0               POINT TO TITLE            JDM1
         LA    R0,L'TITLE0             GET ITS LENGTH            JDM1
         APUT  (R1),(R0),R             WRITE HEADER LINE         JDM1
         LA    R1,TITLE1               POINT TO TITLE            JDM2
         LA    R0,L'TITLE1             GET ITS LENGTH            JDM2
         APUT  (R1),(R0),R             WRITE HEADER LINE         JDM2
         LA    R1,TITLE1+52            POINT TO TITLE            JDM2
         LA    R0,L'TITLE1             GET ITS LENGTH            JDM2
         APUT  (R1),(R0),R             WRITE HEADER LINE         JDM2
         LA    R1,TITLE1+104           POINT TO TITLE            JDM2
         LA    R0,L'TITLE1             GET ITS LENGTH            JDM2
         APUT  (R1),(R0),R             WRITE HEADER LINE         JDM2
         SPACE 1                                                 JDM2
*---------------------------------------------------------------------*
*        ONLY SEARCH FOR BACKGROUND TASKS (CSCB CODE X'02')
*---------------------------------------------------------------------*
         SPACE
         L     R6,CVTMSER              MASTER SCHEDULER DATA AREA
         USING BASE,R6                 ADDRESABILITY
         L     R7,BACHN                CSCB CHAIN POINTER
         USING CSCB,R7                 ADDRESABILITY
CSCBLOOP EQU   *
         MVI   ITEM,C' '               MOVE IN A BLANK           JDM2
         MVC   ITEM+1(LINEL-1),ITEM    ROLL THE BLANK            JDM2
         CLI   CHTRKID,X'02'           IS THIS BATCH JOB         JDM1
         BE    CSCBGO                  YES                       JDM1
         CLI   CHTRKID,X'01'           TSO USER ???              JDM1
         BNE   NEXTCSCB                NO                        JDM1
CSCBGO   EQU   *                                                 JDM1
         SPACE
*---------------------------------------------------------------------*
*        CREATE A TABLE ENTRY FOR THIS JOBNAME
*---------------------------------------------------------------------*
         SPACE
         SR    R5,R5                   CLEAR REG 5
         ICM   R5,3,CHASID             ASID
         CVD   R5,TEMP1                CONVERT TO DECIMAL
         MVC   ASIDNO,ED1              EDIT MASK
         ED    ASIDNO,TEMP1+6          EDIT OUTPUT LINE
         CLI   CHTRKID,X'02'           IS THIS BATCH JOB         JDM1
         BE    BATDISP                 YES                       JDM1
         MVC   JOBNAME,CHKEY           COPY TSOID                JDM1
         MVC   STEPNAME,CHCLS          COPY LOGON PROC NAME      JDM1
         CLI   STEPNAME,X'00'                                    SBG1
         BNE   SKIPBLK1                                          SBG1
         MVI   STEPNAME,X'40'                                    SBG1
SKIPBLK1 DS    0H                                                SBG1
         MVC   PROCSTEP,CHSTEP         COPY PROC STEPNAME        JDM1
         CLC   PROCSTEP,ZEROES                                   SBG1
         BNE   SKIPPRC1                                          SBG1
         MVC   PROCSTEP,BLANKS                                   SBG1
SKIPPRC1 DS    0H                                                SBG1
         B     BATSKIP                                           JDM1
BATDISP  EQU   *                                                 JDM1
         MVC   JOBNAME,CHCLS           COPY JOBNAME
         MVC   STEPNAME,CHSTEP         COPY STEPNAME
         CLI   STEPNAME,X'00'                                    SBG1
         BNE   SKIPBLK2                                          SBG1
         MVI   STEPNAME,X'40'                                    SBG1
SKIPBLK2 DS    0H                                                SBG1
         MVC   PROCSTEP,CHPROCSN       COPY PROC STEPNAME
         CLC   PROCSTEP,ZEROES                                   SBG1
         BNE   SKIPPRC2                                          SBG1
         MVC   PROCSTEP,BLANKS                                   SBG1
SKIPPRC2 DS    0H                                                SBG1
BATSKIP  EQU   *                                                 JDM1
         LR    R8,R10                  ASVT BASE ADDRESS
         S     R5,K1                   ASID - 1
         SLL   R5,2                    MULTIPLY ASID BY 4 (OFFSET)
         AR    R8,R5                   ADD TO ASVT TO GET PROPER SLOT
         L     R8,0(R8)                OBTAIN ASCB ADDRESS
         USING ASCB,R8                 ADDRESSABILITY
         SPACE
         LM    R0,R1,ASCBEJST          CURRENT TCB CPU TIME
         BAL   R14,CONVTIME            CONVERT TO DECIMAL
         MVC   TCBTIME,ED4             EDIT MASK
         ED    TCBTIME-1(8),TEMP1+4    EDIT OUTPUT ENTRY
         LM    R0,R1,ASCBSRBT          CURRENT SRB CPU TIME
         BAL   R14,CONVTIME            CONVERT TO DECIMAL
         MVC   SRBTIME,ED4             EDIT MASK
         ED    SRBTIME-1(8),TEMP1+4    EDIT OUTPUT ENTRY
         TM    ASCBRCTF,ASCBOUT        ASCB SWAPPED OUT?
         BNO   INCR                    NO
         MVI   SWAP,C'S'               YES - SHOW "S"
INCR     EQU   *
         LA    R5,LINEL                GET LINE LENGTH           JDM2
         APUT  (R4),(R5),R             WRITE A LINE
         SPACE
*---------------------------------------------------------------------*
*        LOCATE NEXT CSCB ON CHAIN
*---------------------------------------------------------------------*
         SPACE
NEXTCSCB EQU   *
         L     R7,CHPTR                NEXT CSCB IN CHAIN
         LTR   R7,R7                   DOES IT EXIST?
         BNZ   CSCBLOOP                YES
         SPACE
*---------------------------------------------------------------------*
*        TERMINATE
*---------------------------------------------------------------------*
         SPACE
         LA    R0,AREASIZE             NUMBER OF BYTES TO FREEMAIN
         LR    R1,R13                  ADDRESS OF FREEMAIN AREA
         L     R13,4(R13)
         FREEMAIN R,LV=(0),A=(1)       FREE ACQUIRED MEMORY
         RETURN (14,12),RC=0
         SPACE
         PRINT GEN
         TITLE 'TIME CONVERSION ROUTINE (CONVTIME)'
*---------------------------------------------------------------------*
*                      TIME CONVERSION ROUTINE                        *
*                                                                     *
* R0/R1 CONTAINS THE TIME DOUBLEWORD TO CONVERT.                      *
*                                                                     *
* 8 CHARACTERS ARE CONVERTED INTO LOCATION "TEMP1" (DECIMAL).         *
*                                                                     *
* CALLER'S REGISTERS ARE UNCHANGED UPON RETURN.                       *
*                                                                     *
* ROUTINES CALLED: NONE                                               *
*---------------------------------------------------------------------*
         SPACE
CONVTIME STM   R0,R1,CONVSAVE          SAVE CALLER'S REGS
         SRDL  R0,12                   CONVERT TO MICROSECONDS
         D     R0,K10000               DIVIDE TO GET SECONDS
         CVD   R1,TEMP1                CONVERT TO DECIMAL
         LM    R0,R1,CONVSAVE          RESTORE CALLER'S REGS
         BR    R14                     RETURN TO CALLER
         SPACE
         TITLE 'DATA STORAGE AND CONSTANTS'
*---------------------------------------------------------------------*
*        CONSTANTS
*---------------------------------------------------------------------*
         SPACE
         LTORG
         SPACE
ED1      DC    X'40202020'             EDIT MASK
ED4      DC    XL7'20202021204B20'     EDIT MASK
K1       DC    F'1'                    CONSTANT
K10000   DC    F'10000'                CONSTANT
         SPACE
*---------------------------------------------------------------------*
*        * TITLE LINE SIZE IS IDENTICAL TO ENTRY SIZE *
*---------------------------------------------------------------------*
TITLE    DC    C'          JOB  &&  TSU   DISPLAY   ACTIVE           '
TITLE0   DC    C'          ---  -  ---   -------   ------'
TITLE1   DC    C'                                    S T E P  S T E P'
         DC    C'JOB NAME  ASID  STEPNAME  PROCSTEP  TCB SEC  SRB SEC'
         DC    C'--------  ----  --------  --------  -------  -------'
TITLEN   EQU   *-TITLE1                LENGTH OF TITLE LINES
         SPACE
*---------------------------------------------------------------------*
*        COMPUTE SPACE FOR MAXIMUM OF 72 ASCB'S                  JDM1
*---------------------------------------------------------------------*
         SPACE
         SPACE
         TITLE 'DYNAMIC SAVE AREA'
SAVEAREA DSECT
REGS     DS    18F                     REGISTERS
CONVSAVE DS    D                       TIME CONVERSION REGS
TEMP1    DS    D                       CVD AREA
         SPACE
         DS    C                                                 SBG1
BLANKS   DS    CL40                                              SBG1
         DS    C                                                 SBG1
ZEROES   DS    CL8                                               SBG1
*---------------------------------------------------------------------*
*        * ENTRIES FOR EACH ACTIVE ADDRESS SPACE *
*---------------------------------------------------------------------*
         SPACE
ITEM     DS    0CL52
JOBNAME  DS    CL8                     JOBNAME
         DS    CL1                     BLANK
SWAP     DS    CL1                     SWAP INDICATOR
ASIDNO   DS    CL4                     ASID (EBCDIC)
         DS    CL2                     BLANK
STEPNAME DS    CL8                     STEPNAME
         DS    CL2                     BLANK
PROCSTEP DS    CL8                     PROC STEP NAME
         DS    CL2                     BLANK
TCBTIME  DS    CL7                     TCB TIME
         DS    CL2                     BLANK
SRBTIME  DS    CL7                     SRB TIME
AREASIZE EQU   *-SAVEAREA                                        JDM2
LINEL    EQU   *-ITEM                  LENGTH OF LINE            JDM2
         SPACE
         TITLE 'ADDRESS SPACE CONTROL BLOCK (ASCB)'
         IHAASCB
         SPACE
         TITLE 'ADDRESS SPACE VECTOR TABLE (ASVT)'
         IHAASVT
         SPACE
         TITLE 'COMMAND SCHEDULING CONTROL BLOCK (CSCB)'
CSCB     DSECT
         IEECHAIN
         SPACE
         TITLE 'COMMUNICATIONS VECTOR TABLE (CVT)'
         CVT   SYS=VMS,TSO=YES,DSECT=YES
         SPACE
         TITLE 'MASTER SCHEDULER RESIDENT DATA AREA (BASEA)'
         IEEBASEA
         END
EPUTL    TITLE 'PUTLINE FOR FEWER THAN 83 ARGUMENTS IN THE KEY OF C MAJ&
               OR'
EPUTL    CSECT                                              *HMD 10/85*
         LCLB  &XA                                          *HMD 10/85*
&XA      SETB  1                  SET TO ZERO ON NON/XA     *HMD 10/85*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  MODULE NAME  . . . . EPUTL
*
*  ALIASES  . . . . . . NONE
*
*  DATE WRITTEN . . . . 06/85
*  UPDATED      . . . . 04/88                                  HD APR88
*
*  AUTHOR . . . . . . . JIM ELSWORTH
*  UPDATOR  . . . . . . HOWARD M. DEAN                         HD APR88
*
*  ENVIRONMENT  . . . . TSO COMMAND-PROCESSOR
*
*  PURPOSE:
*    ALLOW AN EASY INTERFACE TO PUTLINE ROUTINE
*
*
*  DESCRIPTION:
*    BUILDS PUTLINE PARAMETER LIST BASED ON INPUT ARGUMENTS.
*    ISSUES SINGLE LINE PUTLINE.
*
*
*  ATTRIBUTES:
*    REENTRANT
*
*  FEATURES:
*    WILL RUN UNDER BATCH TMP SINCE PUTLINE USED INSTEAD OF TPUT.
*
*  CALLED MODULES:
*    PUTLINE
*
*  ASSUMPTIONS:
*    CALLED BY A TSO-CP THAT IS OBSERVING STANDARD LINKAGE
*
*  ABENDS:
*    POSSIBLE 0CX IF BAD ARGUEMNTS
*
*  CAVEATS:
*    UNPREDICTABLE RESULTS IF FIRST PROGRAM RUNNING UNDER THIS TCB
*    DID NOT SAVE (14,12) BEFOR R1 WAS MODIFIED.
*
*
*  INPUT ARGUMENTS:
*    1)  R1 : ADDRESS OF MESSAGE TEXT
*    2)  R0 : LENGTH OF MESSAGE TEXT
*    3) R13 : STANDARD OS SAVE AREA
*    4) R14 : RETURN ADDRESS
*    5) R15 : ENTRY ADDRESS
*
*               ( CONTINUED ... )
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
*  OUTPUT ARGUMENT: RETURN CODE FROM PUTLINE OR
*
RCBA     EQU   100                 BAD MESSAGE ADDRESS
RCBL     EQU   104                 MESSAGE LENGTH <1 OR >MAXMSG
*
MAXMSG   EQU   255                 MAXIMUM MESSAGE LENGTH
*
*
*  REGISTERS USED:
*
R0       EQU   0                   MESSAGE LENGTH AT ENTRY
R1       EQU   1                   MESSAGE ADDRESS AT ENTRY
R2       EQU   2                   WORK
RMA      EQU   4                   @MESSAGE TEXT
RML      EQU   5                   MESSAGE LENGTH
RUPT     EQU   6                   @UPT (USER PROFILE TABLE)
RECT     EQU   7                   @ECT
R12      EQU   12                  CSECT BASE
R13      EQU   13                  @SAVE AREA AND DYNAMIC WORK AREA
R14      EQU   14                  WORK/LINKAGE
R15      EQU   15                  WORK/LINKAGE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         IKJCPPL
         EJECT
         IKJTCB   LIST=YES                                  *HMD 10/85*
         EJECT                                              *HMD 10/85*
         CVT      DSECT=YES,LIST=YES                        *HMD 10/85*
         EJECT
         IHAPSA   LIST=YES                                     HD APR88
         EJECT                                                 HD APR88
PUTLDS   DSECT
         DS    18F                      OS SAVE AREA
PUTAAA   EQU   *
PUTECB   DS    F                        ECB
PUTIOPL  DS    4F                       IOPLADS
PUTBLOK  PUTLINE MF=L
PUTLEN   DS    H                        LENGTH
PUTZZZ   DS    H                        RESERVED
PUTMSG   DS    (MAXMSG)C                MESSAGE TEXT AREA
PUTSIZE  EQU   *-PUTLDS                 SIZE OF PUTLINE PARMS
         EJECT
***********************************************************************
*  SET UP LINKAGE
***********************************************************************
         SPACE 1
EPUTL    CSECT
         AIF  (NOT &XA).X001                                *HMD 10/85*
EPUTL    AMODE 31
.X001    ANOP                                               *HMD 10/85*
         STM   14,12,12(13)       SAVE CALLERS REGS
         LR    R12,R15            COPY EPA
         USING EPUTL,R12          ADDRESSABILITY
         AIF   (NOT &XA).X002                               *HMD 10/85*
         GETMAIN RU,              LV=LENGTH                            &
               LV=PUTSIZE,        STANDARD OS SA PLUS ANY EXTRA        &
               LOC=RES            RELATIVE TO 16MB LINE
         AGO   .X003                                        *HMD 10/85*
.X002    ANOP                                               *HMD 10/85*
         GETMAIN RU,              LV=LENGTH                            &
               LV=PUTSIZE         STANDARD OS SA PLUS ANY EXTRA
.X003    ANOP                                               *HMD 10/85*
         ST    1,8(13)            OUR SAVE AREA POINTER
         ST    13,4(1)            POINTER TO CALLERS SAVE AREA
         LR    13,1               @OUR SAVE AREA
         USING PUTLDS,R13         MAP SA AND WORK AREA
         L     R14,4(R13)         POINT TO CALLERS RSA
         LM    R0,R1,20(R14)      RESTORE REGS CHANGED BY GETMAIN
         SPACE 2
***********************************************************************
*  UNLOAD ARGUMENTS AND VALIDATE WHERE POSSIBLE
***********************************************************************
         SPACE 1
         LA    R15,RCBA                 ASSUME BAD ADDRESS
         LTR   RMA,R1                   MESSAGE ADDRESS
         BNP   RETURN                   GOOD ASSUMPTION
         LA    R15,RCBL                 ASSUME BAD LENGTH
         LTR   RML,R0                   POSITIVE?
         BNP   RETURN                   -NO
         C     RML,=A(MAXMSG)           MORE THAN MAX?
         BH    RETURN                   -YES
         SPACE 2
***********************************************************************
*  BUILD PARAMETER LIST
***********************************************************************
         SPACE 1
         LA    R15,4(RML)               MESSAGE LENGTH
         STH   R15,PUTLEN                 PLUS HEADER
         XC    PUTZZZ,PUTZZZ            CLEAR 2ND HWORD OF RDW
         BCTR  RML,0                    REDUCE FOR MOVE
         EX    RML,PUTMOVE
         EJECT
***********************************************************************
*  DIG UP ADDRESS OF UPT AND ECT
***********************************************************************
         SPACE 1
         L     R2,PSATOLD-PSA           @TCB                   HD APR88
         L     R2,TCBFSA-TCB(R2)        @1ST PROBLEM PGM SAVE AREA
         L     R2,24(R2)                REG1 THAT WAS PASSED TO CP
         USING CPPL,R2                  MAP CPPL
         L     RUPT,CPPLUPT             @UPT
         L     RECT,CPPLECT             @ECT
         SPACE 2
***********************************************************************
*  ISSUE PUTLINE CALL
***********************************************************************
         SPACE 1
         XC    PUTECB,PUTECB            CLEAR ECB
         XC    PUTBLOK(12),PUTBLOK      CLEAR PARM BLOCK       HD APR88
         PUTLINE PARM=PUTBLOK,          PARAMETER BLOCK                &
               UPT=(RUPT),              @UPT                           &
               ECT=(RECT),              @ECT                           &
               ECB=PUTECB,              @ECB                           &
               OUTPUT=(PUTLEN,TERM,SINGLE,DATA),                       &
               MF=(E,PUTIOPL)
         EJECT
***********************************************************************
*  RETURN TO CALLER
***********************************************************************
         SPACE 1
RETURN   EQU   *
         LR    2,R13                    @OUR SAVE AREA
         LR    3,R15                    SAVE RETURN CODE
         L     R13,4(R13)               @CALLERS SAVE AREA
         FREEMAIN RU,LV=PUTSIZE,A=(R2),SP=0
         LR    R15,3                    RESTORE RC
         RETURN (14,12),RC=(15)         RESTORE CALLERS REGS
         SPACE 5
***********************************************************************
*  CONSTANTS AND NON-REENTRANT WORK AREAS
***********************************************************************
         SPACE 1
PUTMOVE  MVC   PUTMSG(*-*),0(RMA)       MOVE MESSAGE TO BUFFER
         SPACE 2
         LTORG
         SPACE 2
         END   EPUTL
